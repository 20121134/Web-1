VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "ClIsisDll"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = False
Attribute VB_Exposed = False
Attribute VB_Ext_KEY = "SavedWithClassBuilder" ,"Yes"
Attribute VB_Ext_KEY = "Top_Level" ,"Yes"
Option Explicit

Const Barra = "/"
Const MaxTamArea = 65000
Const Delim = "{"
Const SEM_ERR = 0

Private ErroIniciacao As Integer

Private H As Long
Private Path As String
Private Nome As String
Private ArqInv As String

Private Msg As ClMsg


Property Let BDPath(v As String)
    Path = v
End Property
Property Get BDPath() As String
    BDPath = Path
End Property
Property Get FullPathBD() As String
    FullPathBD = Path + PathSep + Nome
End Property
Property Get FullPathInv() As String
    FullPathInv = Path + PathSep + IfName
End Property
Property Let BDNome(v As String)
    Nome = v
End Property
Property Get BDNome() As String
    BDNome = Nome
End Property
Property Let IfName(v As String)
    ArqInv = v
End Property
Property Get IfName() As String
    IfName = ArqInv
End Property

Sub AppParSet(File As String)
    Call IsisAppParSet(AppHandle, "@" + File)
End Sub
'-----------------------------------------------------------------------
'MfnQuantity  - retorna o total de Records na base
'Retorno    - total de Records na base
'-----------------------------------------------------------------------
Property Get MfnQuantity() As Long
    Dim ctrl As IsisRecControl
    
    If IsisRecControlMap(H, ctrl) > ZERO Then MfnQuantity = ctrl.nxtmfn - 1
End Property

'-----------------------------------------------------------------------
'Inicia - Inicia a base de dados isis
'pPath  - caminho da base
'pMst   - nome do arquivo da base
'Restart    - reinicia a base de dados
'Retorno    - sucesso ou fracasso
'-----------------------------------------------------------------------
Function Inicia(pPath As String, pMst As String, Label As String, Optional Restart As Boolean) As Boolean
    Dim ret As Boolean
    
    If DirExist(pPath, Label) Then
        If Len(pMst) > 0 Then
            BDPath = pPath
            BDNome = pMst
            If IsisSpaMf(H, FullPathBD) = ERR_FILEMASTER Then
                ErroIniciacao = IsisSpaMfCreate(H)
            Else
                ErroIniciacao = SEM_ERR
                If Restart Then ErroIniciacao = IsisSpaMfCreate(H)
            End If
        End If
    End If
    If ErroIniciacao = SEM_ERR Then
        Call IsisRecShelfSize(H, 0, 3 * MAXMFRL)
        Set Msg = New ClMsg
        ret = True
    End If
    Inicia = ret
End Function

'-----------------------------------------------------------------------
'IfCreate    - Criar o arquivo invertido
'FileName   - Nome do arquivo
'Retorno    - sucesso ou fracasso
'-----------------------------------------------------------------------
Function IfCreate(ByVal FileName As String) As Boolean
    Dim ret As Boolean
    Dim r As Long
    
    IfName = FileName
    r = IsisSpaFst(H, FullPathInv)
    If r = ERR_FILEFST Then
        ret = FileExist(BDPath, IfName + ".fst", "Inverted File")
    ElseIf r = ZERO Then
        r = IsisSpaIf(H, FullPathInv)
        If r = ERR_FILEINVERT Then
            ret = (IsisSpaIfCreate(H) = ZERO)
        ElseIf r = ZERO Then
            ret = True
        End If
    End If
    If Not ret Then MsgBox "Failure creating inverted file " + FullPathInv
    IfCreate = ret
End Function

'-----------------------------------------------------------------------
'RecordSave - Grava em registro
'Value  - conteúdo a ser gravado
'Retorno    - sucesso ou fracasso
'-----------------------------------------------------------------------
Public Function RecordSave(ByVal value As String) As Long
    'Cria e grava um  registro, cujo conteúdo <Value> esteja no formato:
    '   <tag1>campo1</tag1>
    '   <tag2>campo2</tag2>
    '   <tag3>campo3</tag3>
    '   ...
    '   <tagn>campon</tagn>
    'Retorna o Mfn do registro
    Dim ret As Long
    Dim mfn As Long
    
    If ErroIniciacao = SEM_ERR Then
        mfn = IsisRecNew(H, 0)
        If mfn > ZERO Then
            If RecordUpdate(mfn, value) Then ret = mfn
        End If
    End If
    If ret = 0 Then Msg.GiveRunInformation ("Failure writing Mfn=" + CStr(mfn))

    RecordSave = ret
End Function

'-----------------------------------------------------------------------
'FieldContentUpdate - atualizar conteúdo do campo
'Mfn    - registro
'Campo  - campo
'Value  - conteúdo a ser atualizado
'Occ    - ocorrência do campo (opcional, default=1)
'Retorno    - sucesso ou fracasso
'-----------------------------------------------------------------------
Public Function FieldContentUpdate(mfn As Long, Campo As Long, value As String, Optional occ) As Boolean
    Dim ocorrencia As String
    Dim ret As Boolean
    
    If IsMissing(occ) Then
        ocorrencia = "1"
    Else
        ocorrencia = CStr(occ)
    End If
    If IsisRecRead(H, 0, mfn) = ZERO Then
        If IsisRecFieldUpdate(H, 0, "d" + CStr(Campo) + Barra + ocorrencia) = ZERO Then
            If IsisRecFieldUpdate(H, 0, "a" + CStr(Campo) + Delim + value + Delim) = ZERO Then
            'Msg.GiveRunInformation ("Mfn=" + CStr(Mfn))
                If IsisRecWrite(H, 0) = ZERO Then ret = True
            End If
        End If
    End If
    If Not ret Then Msg.GiveRunInformation ("Failure updating Mfn=" + CStr(mfn))
    FieldContentUpdate = ret
End Function

'-----------------------------------------------------------------------
'FieldContentDel - apaga conteúdo do campo
'Mfn    - registro
'Campo  - campo
'Occ    - ocorrência do campo (opcional, default=1)
'Retorno    - sucesso ou fracasso
'-----------------------------------------------------------------------
Public Function FieldContentDel(mfn As Long, Campo As Long, Optional occ) As Boolean
    Dim ocorrencia As String
    Dim ret As Boolean
    
    If IsMissing(occ) Then
        ocorrencia = "1"
    Else
        ocorrencia = CStr(occ)
    End If
    If IsisRecRead(H, 0, mfn) = ZERO Then
        If IsisRecFieldUpdate(H, 0, "d" + CStr(Campo) + Barra + ocorrencia) = ZERO Then
            'Msg.GiveRunInformation ("Mfn=" + CStr(Mfn))
            If IsisRecWrite(H, 0) = ZERO Then ret = True
        End If
    End If
    If Not ret Then Msg.GiveRunInformation ("Failure deleting field " + CStr(Campo) + " in Mfn=" + CStr(mfn))
    FieldContentDel = ret
End Function

'-----------------------------------------------------------------------
'IfUpdate - atualizar o arquivo invertido
'Inicio - registro inicial
'Fim    - registro final
'FlagMantido    -
'Retorno    - sucesso ou fracasso
'-----------------------------------------------------------------------
Function IfUpdate(Inicio As Long, Fim As Long, Optional FlagMantido As Boolean) As Boolean
    Dim op As Long
    Dim r As Long
    If FlagMantido Then op = 1
    Call IsisSpaIf(H, FullPathInv)
    r = IsisRecIfUpdateEx(H, Inicio, Fim, op)
    If r <> ZERO Then Msg.GiveRunInformation ("Failure updating inverted file " + IfName)
    IfUpdate = (r = ZERO)
End Function

'-----------------------------------------------------------------------
'RecordUpdate - atualiza o conteúdo do registro
'Mfn    - registro
'Value  - conteúdo
'Retorno    - sucesso ou fracasso
    'Escreve em um registro cujo Mfn= <mfn>, o Value <Value>
    '   <tag1>campo1</tag1>
    '   <tag2>campo2</tag2>
    '   <tag3>campo3</tag3>
    '   ...
    '   <tagn>campon</tagn>
    'Retorna sucesso ou fracasso
'-----------------------------------------------------------------------
Function RecordUpdate(mfn As Long, value As String) As Boolean
    Dim ret As Boolean
    Dim Mfn2 As Long
    
    If (mfn > MfnQuantity) Then
        While Mfn2 <> mfn
            Mfn2 = IsisRecNew(H, 0)
        Wend
    End If
    
    If IsisRecRead(H, 0, mfn) = ZERO Then
        If IsisRecUpdate(H, 0, value) = ZERO Then
        'Msg.GiveRunInformation ("Mfn=" + CStr(Mfn))
            ret = (IsisRecWrite(H, 0) = ZERO)
        End If
    End If
    If Not ret Then Msg.GiveRunInformation ("Failure updating Mfn=" + CStr(mfn))
    RecordUpdate = ret
End Function

'-----------------------------------------------------------------------
'FieldContentAdd   - adiciona um novo campo a um registro
'Mfn    - registro
'Campo  - campo
'Value  - conteúdo
'Retorno    - sucesso ou fracasso
'-----------------------------------------------------------------------
Function FieldContentAdd(mfn As Long, Campo As Long, value As String) As Boolean
    Dim ret As Boolean
    Dim tam As Long
    Dim area As String * MaxTamArea
    Dim v As String
    
    If IsisRecRead(H, 0, mfn) = ZERO Then
        v = "a" + CStr(Campo) + Delim + value + Delim
        If IsisRecFieldUpdate(H, 0, v) = ZERO Then
        'Msg.GiveRunInformation ("Mfn=" + CStr(Mfn))
            ret = (IsisRecWrite(H, 0) = ZERO)
        End If
    End If
    If Not ret Then Msg.GiveRunInformation ("Failure adding field " + CStr(Campo) + " in Mfn=" + CStr(mfn))
    FieldContentAdd = ret
End Function

Function GetFreeRange(Begin() As Long, Finish() As Long) As Long
    Dim mfn As Long
    Dim RangeCount As Long
    
    mfn = 0
    Erase Begin
    Erase Finish
    While mfn < MfnQuantity
        mfn = mfn + 1
        If FieldCount(mfn) = 0 Then
            RangeCount = RangeCount + 1
            ReDim Preserve Begin(RangeCount)
            ReDim Preserve Finish(RangeCount)
            Begin(RangeCount) = mfn
            
            While (mfn < MfnQuantity) And (Finish(RangeCount) = 0)
                mfn = mfn + 1
                If FieldCount(mfn) > 0 Then
                    Finish(RangeCount) = mfn - 1
                End If
            Wend
            
        End If
    Wend
    If RangeCount > 0 Then
        If (Finish(RangeCount) = 0) And (MfnQuantity = mfn) Then Finish(RangeCount) = mfn
    End If
    GetFreeRange = RangeCount
End Function


'-----------------------------------------------------------------------
'Recorddel    - apaga o conteúdo de um registro
'Mfn    - registro
'Retorno    - sucesso ou fracasso
'-----------------------------------------------------------------------
Function RecordDel(mfn As Long) As Boolean
    Dim ret As Boolean
       
    If IsisRecRead(H, 0, mfn) = ZERO Then
        If IsisRecFieldUpdate(H, 0, "d*") = ZERO Then
        'Msg.GiveRunInformation ("Mfn=" + CStr(Mfn))
            If IsisRecWrite(H, 0) = ZERO Then
                ret = True
            End If
        End If
    End If
    If Not ret Then Msg.GiveRunInformation ("Failure deleting record Mfn=" + CStr(mfn))

    RecordDel = ret
End Function

'-----------------------------------------------------------------------
'RecordGet  - retorna o conteúdo do registro
'Mfn    - registro
'-----------------------------------------------------------------------
Function RecordGet(mfn As Long) As String
    Dim area As String * MaxTamArea
    Dim tam As Long
    Dim ret As String
    
    If IsisRecRead(H, 0, mfn) = ZERO Then
        tam = IsisRecDump(H, 0, area, MaxTamArea)
        If tam > ZERO Then ret = Left(area, tam)
    End If
    RecordGet = ret
End Function

'-----------------------------------------------------------------------
'FieldContentOccGet - retorna o conteúdo do campo
'Mfn    - registro
'Campo  - campo
'Occ    - ocorrência do campo no registro
'Retorno    - o conteúdo do campo
'-----------------------------------------------------------------------
Public Function FieldContentOccGet(mfn As Long, Campo As Long, occ As Long) As String
    Dim area As String * MaxTamArea
    Dim retorno As String
    Dim r As Long
    Dim QtdOcc As Long
    
    r = IsisRecRead(H, 0, mfn)
    If r = ZERO Then
        QtdOcc = IsisRecFieldOcc(H, 0, Campo)
        If (QtdOcc > 0) And (occ <= QtdOcc) Then
            r = IsisRecField(H, 0, Campo, occ, area, MaxTamArea)
            If r > ZERO Then
                retorno = Left(area, r)
                r = InStr(retorno, "^")
                If r > 0 Then retorno = Mid(retorno, 1, r - 1)
            End If
        End If
    End If
    FieldContentOccGet = retorno
End Function


'-----------------------------------------------------------------------
'FieldContentGet - retorna o conteúdo do Field
'Mfn    - registro
'Field  - Field
'Occ    - ocorrência do Field no registro
'Return - o conteúdo do Field
'-----------------------------------------------------------------------
Public Function FieldContentGet(mfn As Long, Field As Long, Contents() As String) As Long
    Dim area As String * MaxTamArea
    Dim aux As String
    Dim r As Long
    Dim QtdOcc As Long
    Dim occ As Long
    
    r = IsisRecRead(H, 0, mfn)
    If r = ZERO Then
        QtdOcc = IsisRecFieldOcc(H, 0, Field)
        ReDim Contents(QtdOcc)
        For occ = 1 To QtdOcc
            r = IsisRecField(H, 0, Field, occ, area, MaxTamArea)
            If r > ZERO Then
                aux = Left(area, r)
                r = InStr(aux, "^")
                If r > 0 Then aux = Mid(aux, 1, r - 1)
                Contents(occ) = aux
            End If
        Next
    End If
    FieldContentGet = QtdOcc
End Function
'-----------------------------------------------------------------------
'SubfieldContentGet - retorna o conteúdo do Subfield
'Mfn    - registro
'Field  - Field
'Subfield   - Subfield (uma letra)
'Contents   - conteúdo
'Retorno    - a quantidade de conteúdo
'-----------------------------------------------------------------------
Public Function SubfieldContentGet(mfn As Long, Field As Long, SubField As String, Contents() As String) As Long
    Dim area As String * MaxTamArea
    Dim retorno As String
    Dim r As Long
    Dim QtdOcc As Long
    Dim occ As Long
    
    r = IsisRecRead(H, 0, mfn)
    If r = ZERO Then
        QtdOcc = IsisRecFieldOcc(H, 0, Field)
        ReDim Contents(QtdOcc)
        For occ = 1 To QtdOcc
            r = IsisRecSubField(H, 0, Field, occ, SubField, area, MaxTamArea)
            If r > ZERO Then retorno = Left(area, r)
            Contents(occ) = retorno
        Next
    End If
            
    SubfieldContentGet = QtdOcc
End Function

'-----------------------------------------------------------------------
'antesFieldContentOccWithSubfieldGet
'FieldContentAllOccGet - retorna o conteúdo do campo com os subcampos
'Mfn    - registro
'Campo  - campo
'Occ    - ocorrência do campo no registro
'Retorno    - o conteúdo do campo
'-----------------------------------------------------------------------
Public Function FieldContentAllOccGet(mfn As Long, Campo As Long, occ As Long) As String
    Dim area As String * MaxTamArea
    Dim retorno As String
    Dim r As Long
    Dim QtdOcc As Long
    
    r = IsisRecRead(H, 0, mfn)
    If r = ZERO Then
        QtdOcc = IsisRecFieldOcc(H, 0, Campo)
        If (QtdOcc > 0) And (occ <= QtdOcc) Then
            r = IsisRecField(H, 0, Campo, occ, area, MaxTamArea)
            If r > ZERO Then retorno = Left(area, r)
        End If
    End If
    FieldContentAllOccGet = retorno
End Function

'-----------------------------------------------------------------------
'SubFieldContentOccGet - retorna o conteúdo do subcampo
'Mfn    - registro
'Campo  - campo
'Occ    - ocorrência do campo no registro
'Subcampo   - subcampo (uma letra)
'Retorno    - o conteúdo do subcampo
'-----------------------------------------------------------------------
Public Function SubFieldContentOccGet(mfn As Long, Campo As Long, occ As Long, Subcampo As String) As String
    Dim area As String * MaxTamArea
    Dim retorno As String
    Dim r As Long
    Dim QtdOcc As Long
    
    r = IsisRecRead(H, 0, mfn)
    If r = ZERO Then
        QtdOcc = IsisRecFieldOcc(H, 0, Campo)
        If (QtdOcc > 0) And (occ <= QtdOcc) Then
            r = IsisRecSubField(H, 0, Campo, occ, Subcampo, area, MaxTamArea)
            If r > ZERO Then retorno = Left(area, r)
        End If
    End If
            
    SubFieldContentOccGet = retorno
End Function

'-----------------------------------------------------------------------
'FieldCount   - Retorna a quantidade de campos de um registro
'Mfn    - registro
'Retorno    - a quantidade de campos
'-----------------------------------------------------------------------
Public Function FieldCount(mfn As Long) As Long
    Dim Qtd As Long
                
    If IsisRecRead(H, 0, mfn) = ZERO Then Qtd = IsisRecNvf(H, 0)

    FieldCount = Qtd
End Function

'-----------------------------------------------------------------------
'TagOccContentGet - obtém os Tags existentes em um registro
'Mfn    - registro
'Tags   - retorno
'Retorno    - a quantidade de Tags
'-----------------------------------------------------------------------
Public Function TagOccContentGet(mfn As Long, Tags() As Long, Occs() As Long, Contents() As String) As Long
    Dim Qtd As Long
    Dim i As Long
    Dim J As Long
    Dim pIsisRecDir() As IsisRecDir
    Dim done() As Boolean
    Dim QtdOcc As Long
    Dim occ As Long
    Dim Campo As Long
    Dim Max As Long
    Dim tam As Long
    Dim area As String * MaxTamArea
    
    If IsisRecRead(H, 0, mfn) = ZERO Then
        Qtd = IsisRecNvf(H, 0)
        If Qtd > 0 Then
            ReDim pIsisRecDir(Qtd)
            ReDim Tags(Qtd)
            ReDim Occs(Qtd)
            ReDim Contents(Qtd)
            If IsisRecDirMap(H, 0, 1, Qtd, pIsisRecDir(0)) > ZERO Then
                For i = 1 To Qtd
                    Campo = pIsisRecDir(i - 1).Tag
                    If Max < Campo Then
                        Max = Campo
                        ReDim Preserve done(Max)
                    End If
                    If Not done(Campo) Then
                        done(Campo) = True
                        QtdOcc = IsisRecFieldOcc(H, 0, Campo)
                        For occ = 1 To QtdOcc
                            J = J + 1
                            tam = IsisRecField(H, 0, Campo, occ, area, MaxTamArea)
                            Contents(J) = Left(area, tam)
                            Tags(J) = Campo
                            Occs(J) = occ
                        Next
                    End If
                Next
            End If
        End If
    End If
    TagOccContentGet = Qtd
End Function

'-----------------------------------------------------------------------
'FieldCount   - Retorna a quantidade (ocorrência) de um determinado campo de um registro
'Mfn    - registro
'Campo  - campo
'Retorno    - ocorrência
'-----------------------------------------------------------------------
Public Function FieldOccCount(mfn As Long, Campo As Long) As Long
    Dim QtdOcc As Long
    
    If IsisRecRead(H, 0, mfn) = ZERO Then QtdOcc = IsisRecFieldOcc(H, 0, Campo)
    FieldOccCount = QtdOcc
End Function

'-----------------------------------------------------------------------
'MfnFind   - retorna os Mfns dos Records correspondentes às chaves
'Termo  - chave
'Mfns   - os Mfns encontrados
'Retorno    - a quantidade de Mfns
'-----------------------------------------------------------------------
Public Function MfnFind(ByVal Termo As String, Mfns() As Long) As Long
    Dim i As Long
    Dim QtdTrm As Long
    Dim pIsisTrmMfn() As IsisTrmMfn
    Dim pIsisTrmRead As IsisTrmRead
    Dim exist As Boolean
    Dim J As Long
    Dim MfnsCount As Long
    
    pIsisTrmRead.key = Termo
    Call IsisSpaIf(H, FullPathInv)
    
    QtdTrm = IsisTrmReadMap(H, 0, pIsisTrmRead)
    If QtdTrm > ZERO Then
        ReDim pIsisTrmMfn(QtdTrm)
        QtdTrm = IsisTrmMfnMap(H, 0, 1, QtdTrm, pIsisTrmMfn(0))
        
        For i = 1 To QtdTrm
            J = 0
            exist = False
            While (J < MfnsCount) And (Not exist)
                J = J + 1
                exist = (pIsisTrmMfn(i - 1).mfn = Mfns(J))
            Wend
            If Not exist Then
                MfnsCount = MfnsCount + 1
                ReDim Preserve Mfns(MfnsCount)
                Mfns(i) = pIsisTrmMfn(i - 1).mfn
            End If
        Next
    End If
    
    MfnFind = MfnsCount
End Function

Public Function GetTerms(Terms() As String, format As String) As Long
    Dim pIsisTrmMfn As IsisTrmMfn
    Dim pIsisTrmRead As IsisTrmRead
    Dim area As String * MaxTamArea
    Dim r As Long
    Dim R1 As Long
    Dim QtdTrm As Long
    
    QtdTrm = 0
    Erase Terms
    
    If IsisSpaIf(H, FullPathBD) = ZERO Then
        If IsisSpaIf(H, FullPathInv) = ZERO Then
            R1 = IsisSpaPftCisis(H, format)
            pIsisTrmRead.key = Chr(32)
            R1 = IsisTrmReadMap(H, 0, pIsisTrmRead)
            While r <> ERR_TRMEOF
                R1 = IsisTrmMfnMap(H, 0, 1, 1, pIsisTrmMfn)
                R1 = IsisRecRead(H, 0, pIsisTrmMfn.mfn)
                R1 = IsisRecFormatCisis(H, 0, area, 900)
                QtdTrm = QtdTrm + 1
                ReDim Preserve Terms(QtdTrm)
                Terms(QtdTrm) = Left(area, R1)
                r = IsisTrmReadNext(H, 0, pIsisTrmRead)
            Wend
        End If
    End If
    GetTerms = QtdTrm
End Function

'-----------------------------------------------------------------------
'AmpliarIntervaloLivre  - dado um intervalo, amplia este se houver Records vazios nas proximidades
'ini    - limite inferior
'fim    - limite superior
'Retorno    - sucesso ou fracasso
'-----------------------------------------------------------------------
Function AmpliarIntervaloLivre(ini As Long, Fim As Long) As Boolean
    Dim Preenchido As Long
    Dim InicioInterv As Long
    Dim FimInterv As Long
    Dim ret As Boolean
    
    InicioInterv = ini
    Preenchido = 0
    While (Preenchido = 0) And (InicioInterv - 1 > 1)
        If IsisRecRead(H, 0, InicioInterv - 1) = ZERO Then
            Preenchido = IsisRecNvf(H, 0)
            If Preenchido = 0 Then InicioInterv = InicioInterv - 1
        End If
    Wend
    If ini > InicioInterv Then
        ini = InicioInterv
        ret = True
    End If
        
    FimInterv = Fim
    Preenchido = 0
    While (Preenchido = 0) And (FimInterv + 1 <= MfnQuantity)
        If IsisRecRead(H, 0, FimInterv + 1) = ZERO Then
            Preenchido = IsisRecNvf(H, 0)
            If Preenchido = 0 Then FimInterv = FimInterv + 1
        End If
    Wend
    If Fim < FimInterv Then
        Fim = FimInterv
        ret = ret Or True
    End If
        
    AmpliarIntervaloLivre = ret
End Function

Private Sub Class_Initialize()
    H = IsisSpaNew(AppHandle)
    ErroIniciacao = -1
End Sub

Private Sub Class_Terminate()
    IsisSpaDelete (H)
End Sub

'-----------------------------------------------------------------------
'IfIdentify  - identifica o arquivo invertido
'Nome   - nome do arquivo invertido
'Retorno    - sucesso ou fracasso
'-----------------------------------------------------------------------
Function IfIdentify(ByVal Nome As String) As Boolean
    Dim ret As Boolean
    Dim r As Long
    
    IfName = Nome
    r = IsisSpaIf(H, FullPathInv)
    If r = ZERO Then
        ret = True
    Else
        ret = False
    End If
    If Not ret Then MsgBox "Erro em IfIdentify."
    IfIdentify = ret
End Function

'-----------------------------------------------------------------------
'Eliminar
'BDCleanAux     - copia a base de dados para uma nova base
'Destino    - nome do arquivo novo
'Retorno    - sucesso ou fracasso
'-----------------------------------------------------------------------
Public Function BDCleanAux(Path As String, File As String) As Boolean
    Dim mfn As Long
    Dim Destino As ClIsisDll
    Dim q As Long
    
    Set Destino = New ClIsisDll
    If Destino.Inicia(Path, File, Chr(32), True) Then
            For mfn = 1 To MfnQuantity
                If FieldCount(mfn) > 0 Then
                    q = q + 1
                    Destino.RecordSave (RecordGet(mfn))
                Else
                Debug.Print
                End If
            Next
    End If
    
    BDCleanAux = (q = Destino.MfnQuantity)
    Set Destino = Nothing
End Function

Public Function BDClean() As Boolean
    Dim deleted As Boolean
    Dim TmpFile As String
    Dim tmp As ClIsisDll
    Dim mfn As Long
    Dim q As Long
    
    TmpFile = BDNome + "TMP"
        
    Set tmp = New ClIsisDll
    If tmp.Inicia(BDPath, TmpFile, Chr(32), True) Then
        For mfn = 1 To MfnQuantity
            If FieldCount(mfn) > 0 Then
                q = q + 1
                tmp.RecordSave (RecordGet(mfn))
            Else
                deleted = True
            End If
        Next
    End If
    Set tmp = Nothing
    
    If deleted Then
        If MfnQuantity > 0 Then
            For mfn = 1 To MfnQuantity
                RecordDel (mfn)
            Next
            Call IfUpdate(1, MfnQuantity)
        End If
        
        If Inicia(BDPath, BDNome, Chr(32), True) Then
            If IfCreate(BDNome) Then
                Set tmp = New ClIsisDll
                If tmp.Inicia(BDPath, TmpFile, Chr(32)) Then
                    For mfn = 1 To tmp.MfnQuantity
                        Debug.Print RecordSave(tmp.RecordGet(mfn))
                    Next
                    Call IfUpdate(1, MfnQuantity)
                    deleted = False
                End If
            End If
            Set tmp = Nothing
        End If
    End If
    Kill BDPath + PathSep + TmpFile + ".*"
    
    BDClean = (Not deleted)
End Function

'-----------------------------------------------------------------------
'BDClean   - remove os Records vazios
'Retorno    - sucesso ou fracasso
'-----------------------------------------------------------------------
Public Function BDClean3() As Boolean
    Dim Records() As String
    Dim ret As Boolean
    Dim mfn As Long
    Dim q As Long
    Dim rangeq As Long
    Dim ri() As Long
    Dim rf() As Long
    Dim i As Long
    
    rangeq = GetFreeRange(ri, rf)
    
    If rangeq > 0 Then
        For i = 1 To rangeq
            While (mfn + 1) < ri(i)
                mfn = mfn + 1
                q = q + 1
                ReDim Preserve Records(q)
                Records(q) = RecordGet(mfn)
            Wend
            mfn = rf(i)
        Next
        mfn = mfn + 1
        For i = mfn To MfnQuantity
            q = q + 1
            ReDim Preserve Records(q)
            Records(q) = RecordGet(i)
        Next
        IsisSpaMfCreate (H)
        IsisSpaIfCreate (H)
        For mfn = 1 To q
            RecordSave (Records(mfn))
        Next
        Erase Records
        Call IfUpdate(1, q)
        ret = (MfnQuantity = q)
    End If
    BDClean3 = ret
End Function

Public Function BDClean2() As Boolean
    Dim Records() As String
    Dim ret As Boolean
    Dim mfn As Long
    Dim reg As String
    Dim q As Long
       
    For mfn = 1 To MfnQuantity
        reg = RecordGet(mfn)
        If Len(reg) > 0 Then
            q = q + 1
            ReDim Preserve Records(q)
            Records(q) = reg
        End If
    Next
    
    If q < MfnQuantity Then
        IsisSpaMfCreate (H)
        IsisSpaIfCreate (H)
        For mfn = 1 To q
            RecordSave (Records(mfn))
        Next
        If q > 0 Then Call IfUpdate(1, q)
        ret = (MfnQuantity = q)
    ElseIf q = MfnQuantity Then
        ret = True
    End If
    Erase Records

    BDClean2 = ret
End Function

'-----------------------------------------------------------------------
'BDDel  - apaga a base de dados
'Retorno    - sucesso ou fracasso
'-----------------------------------------------------------------------
Function BDDel() As Boolean
    Dim ret As Boolean
    
    If IsisSpaMf(H, FullPathBD) = ZERO Then
        If (IsisSpaMfCreate(H) = ZERO) Then
            If IsisSpaIf(H, FullPathInv) = ZERO Then ret = (IsisSpaIfCreate(H) = ZERO)
        End If
    End If
    BDDel = ret
End Function

Function RecordCopy(SourceMfn As Long, DestinyBD As ClIsisDll, DestinyMfn As Long) As Boolean
    Dim ret As Boolean
    Dim registro As String
    
    'Obtenção do registro
    If (SourceMfn > 0) And (SourceMfn <= MfnQuantity) Then
        registro = RecordGet(SourceMfn)
    
        If (DestinyMfn = 0) Then
            DestinyMfn = DestinyBD.RecordSave(registro)
            ret = (DestinyMfn > 0)
        Else
            ret = DestinyBD.RecordUpdate(DestinyMfn, registro)
        End If
    
        If ret Then
            If Not DestinyBD.IfUpdate(DestinyMfn, DestinyMfn) Then
                Call Msg.GiveRunInformation("Failure updating " + DestinyBD.BDNome)
                ret = False
            End If
        Else
            Call Msg.GiveRunInformation("Failure copying record.")
        End If
    End If
    RecordCopy = ret
End Function

Function MfnFindOne(Chave As String) As Long
    Dim xMfn() As Long
    Dim mfn As Long
    Dim i As Long
    
    i = MfnFind(Chave, xMfn)
    If i > 0 Then mfn = xMfn(i)
        
    MfnFindOne = mfn
End Function

Function Save(Contents As String, mfn As Long) As Boolean
    Dim ret As Boolean
    
    If mfn = 0 Then
        mfn = RecordSave(Contents)
        If mfn > 0 Then ret = IfUpdate(mfn, mfn)
    Else
        If RecordUpdate(mfn, Contents) Then ret = IfUpdate(mfn, mfn)
    End If
    Save = ret
End Function


'-----------------------------------------------------------------------
'nao existe no conversor
'FieldTagGet - obtém os campos existentes em um registro
'Mfn    - registro
'Campos - retorno
'Retorno    - a quantidade de campos
'-----------------------------------------------------------------------
Public Function FieldTagGet(mfn As Long, Campos() As Long) As Long
    Dim Qtd As Long
    Dim i As Long
    Dim pIsisRecDir() As IsisRecDir
    
    If IsisRecRead(H, 0, mfn) = ZERO Then
        Qtd = IsisRecNvf(H, 0)
        ReDim pIsisRecDir(Qtd)
        ReDim Campos(Qtd)
        Call IsisRecDirMap(H, 0, 1, Qtd, pIsisRecDir(0))
        For i = 1 To Qtd
            Campos(i) = pIsisRecDir(i - 1).Tag
        Next
    End If
    FieldTagGet = Qtd
End Function


'-----------------------------------------------------------------------
'nao existe no conversor
'antesFieldContentGetAll
'FieldContentAllGet - retorna o conteúdo do Field
'Mfn    - registro
'Field  - Field
'Occ    - ocorrência do Field no registro
'Return - o conteúdo do Field
'-----------------------------------------------------------------------
Public Function FieldContentAllGet(mfn As Long, Field As Long, Contents() As String) As Long
    Dim area As String * MaxTamArea
    Dim r As Long
    Dim QtdOcc As Long
    Dim occ As Long
    
    r = IsisRecRead(H, 0, mfn)
    If r = ZERO Then
        QtdOcc = IsisRecFieldOcc(H, 0, Field)
        ReDim Contents(QtdOcc)
        For occ = 1 To QtdOcc
            r = IsisRecField(H, 0, Field, occ, area, MaxTamArea)
            If r > ZERO Then Contents(occ) = Left(area, r)
        Next
    End If
    FieldContentAllGet = QtdOcc
End Function

Sub Gizmo(File As String)
    Dim mfn As Long
'    Dim fn As Long
'    Dim FileName As String
    Dim p As Long
    Dim NewValue As String
    Dim area As String * MaxTamArea
    Dim tam As Long
    
    If IsisSpaGf(H, File) = ZERO Then
        Call IsisSpaGf(H, File)
        For mfn = 1 To MfnQuantity
        'Debug.Print mfn
        
            If IsisRecRead(H, 0, mfn) = ZERO Then
                tam = IsisRecDump(H, 0, area, MaxTamArea)
                If tam > ZERO Then
                    NewValue = Left(area, tam)
                    'Debug.Print NewValue
                    If IsisRecUpdate(H, 0, NewValue) = ZERO Then
                        If IsisRecWrite(H, 0) <> ZERO Then
                        End If
                    End If
                End If
            End If
        Next
    End If
    'Close fn
End Sub

Public Function UsePft(mfn As Long, format As String) As String
    Dim area As String * MaxTamArea
    Dim ResultLen As Long
    Dim Result As String
    
    ' If IsisSpaMf(H, FullPathBD) = ZERO Then
        'Call IsisRecShelfSize(H, 0, 3 * MAXMFRL)
        If IsisSpaPftCisis(H, format) = ZERO Then
            If IsisRecRead(H, 0, mfn) = ZERO Then
                ResultLen = IsisRecFormatCisis(H, 0, area, MaxTamArea)
                If ResultLen > 0 Then Result = Left(area, ResultLen)
            End If
        End If
    'End If
    UsePft = Result
End Function

Public Function UseProc(mfn As Long, format As String) As Boolean
    Dim ret As Boolean
    
    'If IsisSpaMf(H, FullPathBD) = ZERO Then
        If IsisRecRead(H, 0, mfn) = ZERO Then
            If IsisRecFieldUpdate(H, 0, format) = ZERO Then
                If IsisRecWrite(H, 0) = ZERO Then ret = True
            End If
        End If
    'End If
    UseProc = ret
End Function
